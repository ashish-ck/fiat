apply plugin: 'io.spinnaker.package'

ext {
  springConfigLocation = System.getProperty('spring.config.additional-location', "${System.getProperty('user.home')}/.spinnaker/".toString())
  springProfiles = System.getProperty('spring.profiles.active', "test,local")
}

run {
  systemProperty('spring.config.additional-location', project.springConfigLocation)
  systemProperty('spring.profiles.active', project.springProfiles)
}
mainClassName = 'com.netflix.spinnaker.fiat.Main'

configurations.all {
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: "org.slf4j", module: "slf4j-log4j12"
}

dependencies {
  implementation "com.squareup.retrofit:retrofit"
  implementation "com.squareup.retrofit:converter-jackson"
  implementation "com.squareup.okhttp:okhttp"
  implementation "com.squareup.okhttp:okhttp-urlconnection"
  implementation "com.squareup.okhttp:okhttp-apache"
  implementation "com.jakewharton.retrofit:retrofit1-okhttp3-client"
  implementation "com.google.guava:guava"

  implementation "org.springframework.boot:spring-boot-properties-migrator"
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation "io.spinnaker.kork:kork-plugins"
  implementation "io.spinnaker.kork:kork-web"
  implementation "io.swagger:swagger-annotations"
  implementation "net.logstash.logback:logstash-logback-encoder"

  runtimeOnly "io.spinnaker.kork:kork-runtime"

  // To fix https://nvd.nist.gov/vuln/detail/CVE-2021-21350
  //        https://cve.mitre.org/cgi-bin/cvename.cgi?name=CVE-2021-29505
  // Introduced by com.netflix.eureka:eureka-client via kork-eureka
  //
  // TODO: Move to kork
  // when we build kork ourselves, update in spinnaker-dependencies/spinnaker-dependencies.gradle. And remove completely when
  // https://github.com/Netflix/eureka releases a version that contains com.thoughtworks.xstream:1.4.17, and kork uses it.

  // To fix https://nvd.nist.gov/vuln/detail/CVE-2019-17495
  // Introduced by io.springfox:springfox-swagger-ui via kork-bom and kork-swagger
  // TODO: Move to kork, update version

  // To fix https://github.com/cbeust/testng/issues/2150 (sonatype-2019-0115)
  // Introduced by org.testng used by org.codehaus.groovy:groovy-testng via kork-web
  // TODO: Move to kork by upgrading the version of groovy and spockframework
  constraints {
     runtimeOnly("com.thoughtworks.xstream:xstream:1.4.17")

     runtimeOnly("io.springfox:springfox-swagger-ui:2.10.0")

     implementation("org.codehaus.groovy:groovy-all:3.0.8")
     implementation("org.spockframework:spock-core:2.0-groovy-3.0")
  } // code-fix end

  implementation project(':fiat-core')
  implementation project(':fiat-roles')

  // Add each included authz provider as a runtime dependency
  gradle.includedProviderProjects.each {
    implementation project(it)
  }

  testImplementation "com.netflix.spinnaker.kork:kork-jedis"
  testImplementation "com.netflix.spinnaker.kork:kork-jedis-test"
}

applicationName = 'fiat'
